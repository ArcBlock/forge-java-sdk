plugins {
  id 'java-library'
  id 'org.jetbrains.kotlin.jvm' version '1.3.31'
  id "org.springframework.boot" version "2.1.8.RELEASE" apply(false)
}
apply plugin: 'io.spring.dependency-management'


group 'io.arcblock.forge'
version project.file("../version").readLines().first()
jar {
  baseName = "abtdid-spring-boot-starter"
  version = project.file("../version").readLines().first()
}

dependencyManagement {
  imports {
    mavenBom org.springframework.boot.gradle.plugin.SpringBootPlugin.BOM_COORDINATES
  }
}

sourceCompatibility = 1.8

repositories {
  jcenter()
  mavenCentral()
  mavenLocal()
}

dependencies {
  api project(":did")
  implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
  implementation "org.apache.commons:commons-text:1.8"
  implementation "org.springframework.boot:spring-boot-autoconfigure"
  //implementation "org.springframework.boot:spring-boot-configuration-processor"
  testCompile group: 'junit', name: 'junit', version: '4.12'
}


sourceSets {
  main {

    java { srcDirs ("src/main/java",) }

  }
  test {

    java { srcDir "src/test/java" }

  }
}


compileKotlin {
  kotlinOptions.jvmTarget = "1.8"
}
compileTestKotlin {
  kotlinOptions.jvmTarget = "1.8"
}

javadoc {
  enabled = false
}



artifacts {
  archives jar
//  archives sourceJar
//  archives javadocJar
}

signing {
  sign configurations.archives
}


publishing {
  repositories {
    maven {
      def releaseUrl = "https://oss.sonatype.org/service/local/staging/deploy/maven2"//"s3://android-docs.arcblock.io.s3.amazonaws.com/release"
      def snapshotUrl = "https://oss.sonatype.org/content/repositories/snapshots"
      url releaseUrl
      def un = (properties.hasProperty("sonatypeUsername"))?properties.get("sonatypeUsername").toString():""
      def pw = (properties.hasProperty("sonatypePassword"))?properties.get("sonatypePassword").toString():""
      credentials {
        username sonatypeUsername
        password sonatypePassword
      }
    }

  }

  publications {
    mavenJava(MavenPublication) {
      customizePom(pom)
      groupId rootProject.group
      artifactId 'abtdid-spring-boot-starter'
      version rootProject.version
      from components.java
      // create the sign pom artifact
      pom.withXml {
        def pomFile = file("${project.buildDir}/generated-pom.xml")
        writeTo(pomFile)
        def pomAscFile = signing.sign(pomFile).signatureFiles[0]
        artifact(pomAscFile) {
          classifier = null
          extension = 'pom.asc'
        }
      }
// create the signed artifacts
      project.tasks.signArchives.signatureFiles.each {
        artifact(it) {
          def matcher = it.file =~ /-(sources|javadoc)\.jar\.asc$/
          if (matcher.find()) {
            classifier = matcher.group(1)
          } else {
            classifier = null
          }
          extension = 'jar.asc'
        }
      }

    }
  }
}
def customizePom(pom) {
  pom.withXml {
    def root = asNode()
    // add all items necessary for maven central publication
    root.children().last() + {
      resolveStrategy = Closure.DELEGATE_FIRST

      description 'Forge java sdk'
      name 'Forge Java SDK '
      url 'https://github.com/Arcblock/Forge-java-sdk'
      organization {
        name 'Arcblock.io'
        url 'https://github.com/Arcblock'
      }
      issueManagement {
        system 'GitHub'
        url 'https://github.com/Arcblock/Forge-java-sdk/issues'
      }
      licenses {
        license {
          name 'Apache License 2.0'
          url 'https://github.com/ArcBlock/forge-java-sdk/blob/master/LICENSE'
          distribution 'repo'
        }
      }
      scm {
        url 'https://github.com/ArcBlock/forge-java-sdk'
        connection 'scm:git:git://github.com/ArcBlock/forge-java-sdk.git'
        developerConnection 'scm:git:ssh://git@github.com:ArcBlock/forge-java-sdk.git'
      }
      developers {
        developer {
          name 'Arcblock'
        }
      }
    }
  }
}


model {
  tasks.generatePomFileForMavenJavaPublication {
    destination = file("$buildDir/generated-pom.xml")
  }
  tasks.publishMavenJavaPublicationToMavenLocal {
    dependsOn project.tasks.signArchives
  }
  tasks.publishMavenJavaPublicationToMavenRepository {
    dependsOn project.tasks.signArchives
  }
}








